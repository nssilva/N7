' draw poly image xform
' ---------------------

' Use the compiler flag 'win32' to hide the console window.
#win32

' Create a window of the size 640x480.
set window "draw image", 640, 480
set redraw off

' Load a 64x64 pixel plasma image.
img = loadimage("assets/plasma.png")

' Create points for a weird polygon with its origin at 0, 0. Each point has 4 values: x, y, u and v.
' X and y are screen coordinates, while u and v are image coordinates (x and y but in image space).
points = [
        -50, -50, 0, 0, ' First point: x, y, u, v
        50, -50, 64, 0, ' Second point ...
        25, 0, 48, 32,
        50, 50, 64, 64,
        -50, 50, 0, 64,
        -25, 0, 16, 32]

' Angle for rotation.
angle = 0
 
while not keydown(KEY_ESCAPE, true)
    ' Update angle.
    angle = (angle + 1)%360

    ' Clear screen gray.
    set color 64, 64, 64
    cls
    
    ' Draw a textured and transformed polygon at the center of the window.
    set color 255, 255, 255
    ' 'draw poly image xform image_id, points, x, y, scale_x, scale_y, angle, pivot_x, pivot_y[,
    ' field_count[, point_count]]' draws a transformed textured polygon using the image image_id and
    ' an array of points in the format [x0, y0, u0, v0, x1, y1, u1, v1 .. xn, yn, un, vn]. The u and
    ' v coordinates are in image space, while the x and y coordinates are screen coordinates. The
    ' polygon will be translated to the position x, y, scaled by scale_x and scale_y and rotated
    ' angle radians. If you want the polygon to be transformed around a different point than 0, 0,
    ' you can use pivot_x and pivot_y. Unless you want to write your own 3d renderer, the optional
    ' field_count should always be 4. If you don't want to use all the points in the array, you can
    ' set point_count to the number of points you want to use.
    draw poly image xform img, points, 320, 240, 1, 1, 0, 0, 0
    
    ' Draw a spinning polygon on the left side of the window.
    draw poly image xform img, points, 160, 240, 1, 1, rad(angle), 0, 0
    
    ' Draw a transparent polygon that is rotated and scaled around its top center on the right.
    set color 255, 255, 255, 128
    draw poly image xform img, points, 480, 240, 2.0, 3.0, rad(angle), 0, -50
    
    set caret 320, 460
    set color 255, 255, 255
    center "Press escape to quit ..."
    
    redraw
    fwait 60
wend
