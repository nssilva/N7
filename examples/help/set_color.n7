' set color
' ---------

' Use the compiler flag 'win32' to hide the console window.
#win32

' Create a window.
set window "set color", 640, 480

' 'set color r, g, b' sets the color for all future draw commands in RGB format, where r, g, and
' b are the red, green and blue intensities, all in the range [0..255]. Here we clear the screen
' and draw some primitives with different colors.
set color 64, 64, 64 ' Dark gray.
cls

set color 255, 200, 0 ' Orange.
draw rect 110, 190, 100, 100, true

set color 255, 80, 255 ' Pink.
draw ellipse 320, 240, 50, 50, true

set color 64, 255, 64 ' Green.
draw poly [480, 190, 530, 290, 430, 290], true

' 'set color r, g, b, a' sets the color for all future draw commands in RGBA format, where a is
' the alpha value. An alpha value of 255 means full opacity, while 0 is fully transparent.
set color 255, 255, 255, 128 ' Half transparent white.
draw rect 160, 240, 320, 100, true

' 'set color c' sets the color using three values from an array, [r, g, b] or [r, g, b, a].
cyan = [0, 255, 255]
set color cyan
set caret 320, 448
center "Press Esc to quit ..."

' Loop until Esc key is pressed.
while not keydown(KEY_ESCAPE)  fwait 60
